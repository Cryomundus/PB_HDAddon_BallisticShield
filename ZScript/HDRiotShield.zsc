//-------------------------------------------------
// Ballistic Shield
//-------------------------------------------------

const HDLD_RIOTSHIELD="shd";
const ENC_PBRIOTSHIELD=165;

//NO durability. I don't want to give myself another headache since figuring out
//how to make this thing work is enough of a hassle already.

class HDRiotShieldItem:HDPickup{
	bool activated;
	HDRiotShieldExt shieldActor; 
	
	default{
		inventory.icon "RSH0Z0";
		inventory.pickupmessage "Picked up a ballistic shield.";
		height 10;radius 20;
		hdpickup.bulk ENC_PBRIOTSHIELD;
		hdpickup.refid HDLD_RIOTSHIELD;
		+inventory.invbar
		+hdpickup.notinpockets
		-hdpickup.fitsinbackpack
		inventory.maxamount 1;
		tag "Ballistic Shield";
	}
	
	override void DetachFromOwner() {
		if (activated) { activated=false; }
		super.DetachFromOwner();
	}
	
	states{
	spawn:
		RSH0 Z -1 nodelay 
		{
			A_SetPitch(270);
			A_SetAngle(frandom(0,360));
		}
		stop;
	use:
		TNT1 A 0 
		{
			if(!invoker.activated)
			{
				A_Overlay(5,"shield.spawn",true);
				invoker.activated=true;
			}
			else
			{
				//A_ClearOverlays(5,5);
				A_Overlay(5,"shield.evaporate",true);
			}
			
			if(player){
			player.cmd.buttons|=BT_USE;}
		}fail;
	
	shield.spawn:
		TNT1 A 32 
		{
			A_Log("Deploying shield...");
			let hdp=hdplayerpawn(self);
			hdp.stunned+=52;
		}
	shield.ready:
		TNT1 A 1
		{
		A_Log("Ready! Use item again to stow away.");
		 A_Overlay(4,"shield.fullyready");
		}
		stop;
	shield.fullyready:
		TNT1 A 1{
			
			if (!invoker.shieldActor)
			{
				invoker.shieldActor = HDRiotShieldExt(Actor.Spawn("HDRiotShieldExt",
				pos + (AngleToVector(angle, 35), pos.z), ALLOW_REPLACE));
				
				if (invoker.shieldActor)
				{
					invoker.shieldActor.master = invoker;
					invoker.shieldActor.target = self;
				}
			}
		}loop;
	shield.evaporate:
		TNT1 A 32 
		{
			A_Log("Stowing away shield...");
			let hdp=hdplayerpawn(self);
			hdp.stunned+=52;
		}
		TNT1 A 1
		{
			A_ClearOverlays(4,4);
			if (invoker.shieldActor)
			{
				invoker.shieldActor.destroy();
			}
			A_Log("Shield stored.");
			invoker.activated=false;
		}
		stop;
	}
}

class HDRiotShieldExt:HDActor{
	int uptime;
	default{
	+shootable +noblood +dontthrust
	+solid +interpolateangles +slidesonwalls
	PainChance 0;
	mass 15;
	radius 12;
	Height 50;
	health 150;
	damagefactor "hot", 0.01;
	damagefactor "balefire", 2;
	damagefactor 0.5;
	species "HDPlayerPawn";
	}
	
	override double bulletresistance(
		double hitangle //abs(bullet.angleto(hitactor),bullet.angle)
	){
		return max(0.0,frandom(0.9,1.0)-hitangle*0.5);
	}
	
	
	override void Tick() //josh771's changes
	{
		super.Tick();
		let plr = HDPlayerPawn(target);
		if (health<spawnhealth())
		{health+=5;}
		
		if(health>spawnhealth())
		{
			health=spawnhealth();
		}
		
		if(plr && uptime>32 && Distance3D(plr) > 64)
		{
			plr.A_DropInventory("HDRiotShieldItem");
			plr.A_Log("Butter fingers...");
		}
		else if (uptime<32)
		{
			uptime++;
			self.damagemobj(self,plr,7,"bashing");
		}
		
		if (plr)
		{
			if(plr.player.readyweapon)
			{
				if((plr.player.readyweapon is "HDFist")
				||(plr.player.readyweapon is "HDWeaponGrabber")
				||(plr.player.readyweapon is "HDHandgun")
				||(plr.player.readyweapon is "HDGrenadeThrower")
				||(plr.player.readyweapon is "Blooper")
				||(plr.player.readyweapon is "NullWeapon"))
				{
					plr.player.readyweapon.lookscale=0.35;
					plr.stunned++;
				}
				else// if
				{
					plr.A_Log("Can't use that with the shield.");
					plr.A_DropInventory("HDRiotShieldItem");
				}
			}
		}
		
		if (health<15)
		{
			if (plr)
			{
				plr.A_Log("Shield's taken too much damage!");
				plr.A_DropInventory("HDRiotShieldItem");
			}
		}
		if (!plr)
			Destroy();
		
		else
		{
		Vector3 dest;
		dest.xy = plr.pos.xy + AngleToVector(plr.gunangle+plr.leaned*5.85, 36);
		dest.z = plr.pos.z - 26 - sin(plr.pitch+45)*27 + 53 * plr.player.crouchfactor;
		vel = dest - pos + plr.vel;
		angle = plr.gunangle+plr.leaned*2;
		pitch = (plr.pitch*0.5)+plr.gunpitch*0.25;
		
		let hdrs = plr.checkinventory("HDRiotShieldItem",1);
		if (!hdrs)
			Destroy();
		}
	}
	
	states{
	spawn:
		RSH0 Z 1 nodelay;
		loop;
	pain:
	//	RSH0 Z 2 A_StartSound("misc/riotshield/hit",chan_body,chanf_overlap);
	//	goto spawn;
	pain.bashing:
	pain.fire:
		RSH0 Z 1;
		goto spawn;
	Death:
		RSH0 Z 1
		{
			let plr = HDPlayerPawn(target);
			if (plr)
			{
				plr.A_DropInventory("HDRiotShieldItem");
			}
		}
		goto spawn;
	}
}


//For testing purposes
class HDRiotShield_Permanent:HDRiotShieldExt{
	default{
	mass 40;
	gravity 1;
	species "riotshield";
	}
	
	override void Tick() //josh771's changes
	{
		HDActor.Tick();
	}
	states{
	spawn:
		RSH0 Z -1;
		loop;
	pain:
		RSH0 Z 1;
		goto spawn;
	}
}
/*
class HDRiotShield_SideHitbox:HDRiotShieldExt{
	default{
	mass 40;
	radius 10;
	Height 65;
	health 900000;
	damagefactor 0.1;
	species "riotshield";
	}
	states{
	spawn:
		TNT1 A 2;
		stop;
	Death:
		TNT1 A 1;
		stop;
	}
}
*/